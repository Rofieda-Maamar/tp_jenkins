plugins {
    id 'java'
    id 'application'
    id 'maven-publish'
    id "org.sonarqube" version "4.3.0.3225" // latest compatible version
    id 'jacoco'
}

group = 'com.example'
version = '1.0-SNAPSHOT'

// ===== Maven credentials / repo =====
def mavenUrl = project.findProperty('maven.url') ?: System.getenv('maven.url') ?: System.getenv('MAVEN_URL') ?: System.getenv('url') ?: ''
def mavenUsername = project.findProperty('maven.username') ?: System.getenv('maven.username') ?: System.getenv('MAVEN_USERNAME') ?: System.getenv('username') ?: ''
def mavenPassword = project.findProperty('maven.password') ?: System.getenv('maven.password') ?: System.getenv('MAVEN_PASSWORD') ?: System.getenv('password') ?: ''

repositories {
    mavenCentral()

    if (mavenUrl) {
        maven {
            url = uri(mavenUrl)
            if (mavenUsername || mavenPassword) {
                credentials {
                    username = mavenUsername
                    password = mavenPassword
                }
            }
        }
    }
}

application {
    mainClass = 'com.example.Main'
}

dependencies {
    // JUnit
    testImplementation 'junit:junit:4.13.2'

    // Cucumber
    testImplementation 'io.cucumber:cucumber-java:7.31.0'
    testImplementation 'io.cucumber:cucumber-junit:7.31.0'
}

// ===== Test Configuration =====
test {
    useJUnit() // JUnit 4
    testLogging {
        events "passed", "skipped", "failed"
    }

    // Generate test reports in multiple formats
    reports {
        html.required = true
        junitXml.required = true
    }
}
jacoco {
    toolVersion = "0.8.8"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}
// ===== Cucumber Configuration =====
task cucumber(type: Test) {
    useJUnit()
    systemProperty "cucumber.filter.tags", System.getProperty("cucumber.filter.tags")

    // Add source directories for Cucumber
    include '**/*Cucumber*.*', '**/*Test.*'
    exclude '**/*MainTest.*'

    reports {
        html.required = true
        junitXml.required = true
    }
}

// ===== Maven Publishing =====
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = project.name
            version = project.version
        }
    }
    repositories {
        if (mavenUrl) {
            maven {
                url = uri(mavenUrl)
                credentials {
                    username = mavenUsername
                    password = mavenPassword
                }
            }
        }
    }
}
